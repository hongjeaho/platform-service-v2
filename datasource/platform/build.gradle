buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.flywaydb:flyway-mysql:9.22.0")
    }
}

plugins {
    id "org.flywaydb.flyway" version "9.22.0"
    id "nu.studer.jooq" version "9.0"
}

description = "platform-datasource module"

dependencies {
    // spring transaction (to fix compilation warnings)
    api 'org.springframework:spring-tx'

    // mybatis
    implementation("org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.4")

    runtimeOnly 'com.mysql:mysql-connector-j'

    // jooq
    implementation('org.springframework.boot:spring-boot-starter-jooq') {
        exclude group: 'org.jooq'
    }

    implementation "org.jooq:jooq:${jooqVersion}"
    jooqGenerator(project(":common-jooq"))
    jooqGenerator 'com.mysql:mysql-connector-j'
    jooqGenerator "org.jooq:jooq:${jooqVersion}"
    jooqGenerator "org.jooq:jooq-meta:${jooqVersion}"
    jooqGenerator "org.jooq:jooq-codegen:${jooqVersion}"

    testImplementation(testFixtures(project(":common-core")))
}

// 모듈별 properties 파일 로드
def moduleProperties = new Properties()
def modulePropsFile = file('gradle.properties')
if (modulePropsFile.exists()) {
    modulePropsFile.withInputStream { moduleProperties.load(it) }
}

var DATABASE_DRIVER = "com.mysql.cj.jdbc.Driver"
var PLATFORM_DB_URL = System.getenv("PLATFORM_DB_URL") ?: moduleProperties['db.url']
var PLATFORM_DB_USER = System.getenv("PLATFORM_DB_USER") ?: moduleProperties['db.user']
var PLATFORM_DB_PWD = System.getenv("PLATFORM_DB_PWD") ?: moduleProperties['db.password']

// DB 연결정보 필수 확인 (CI/CD 환경에서는 환경변수 사용)
var isCI = System.getenv("CI") == "true"
if (!PLATFORM_DB_URL || !PLATFORM_DB_USER || !PLATFORM_DB_PWD) {
    if (isCI) {
        throw new GradleException("CI 환경에서는 DB 연결정보 환경변수가 필요합니다: PLATFORM_DB_URL, PLATFORM_DB_USER, PLATFORM_DB_PWD")
    } else {
        throw new GradleException("DB 연결정보가 필요합니다. 환경변수를 설정하거나 gradle.properties 파일에 db.url, db.user, db.password를 추가하세요.")
    }
}

jooq {
    version = jooqVersion
    edition = nu.studer.gradle.jooq.JooqEdition.OSS // the default (can be omitted)

    configurations {
        main {
            generateSchemaSourceOnCompilation = false

            generationTool {
                jdbc {
                    driver = DATABASE_DRIVER
                    url = PLATFORM_DB_URL
                    user = PLATFORM_DB_USER
                    password = PLATFORM_DB_PWD
                }

                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'

                    database {
                        name = "org.jooq.meta.mysql.MySQLDatabase"
                        inputSchema = "store"
                        unsignedTypes = true
                        excludes = """
                            flyway_schema_history
                            | batch_job_.*
                            | batch_step_.*
                        """
                        forcedTypes {
                            forcedType {
                                name = "BOOLEAN"
                                includeTypes = "(?i:(TINY|SMALL|MEDIUM|BIG)?INT(UNSIGNED)?\\(1\\))"
                            }
                        }
                    }

                    generate {
                        daos = true                       // DSL 생성시 dao 생성 여부
                        records = true                    // DSL 생성시 record 생성 여부
                        fluentSetters = true              // 메서드 체이닝 가능한 setter 생성
                        javaTimeTypes = true              // Java 8 날짜/시간 타입 사용
                        deprecated = false                // deprecated 코드 생성 안함
                        pojos = true                      // POJO 생성 추가
                        interfaces = true                 // 인터페이스 생성 추가
                    }

                    target {
                        directory = "${buildDir}/generated/sources/jooq/main"
                        packageName = "com.platform.datasource.platform.jooq.generated"
                        clean = true
                    }

                    strategy.name = "com.platform.common.jooq.CustomGeneratorStrategy"
                }
            }
        }
    }
}

flyway {
    url = PLATFORM_DB_URL
    user = PLATFORM_DB_USER
    password = PLATFORM_DB_PWD
    locations = ["filesystem:${file('flyway').absolutePath}"]
    encoding = 'UTF-8'
    cleanDisabled = System.getenv("CI") == "true"
    validateOnMigrate = true
    outOfOrder = true
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "${buildDir}/generated/sources/jooq/main"]
        }
    }
}

// compileJava 전에 generateJooq 실행
tasks.named('compileJava') {
    dependsOn tasks.named('generateJooq')
}
